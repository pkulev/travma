(include SDL3/SDL.h)
(include SDL3/SDL_main.h)

;;; Alsmost cheating
(define NK_INCLUDE_FIXED_TYPES)
(define NK_INCLUDE_STANDARD_IO)
(define NK_INCLUDE_STANDARD_VARARGS)
(define NK_INCLUDE_DEFAULT_ALLOCATOR)
(define NK_INCLUDE_VERTEX_BUFFER_OUTPUT)
(define NK_INCLUDE_FONT_BAKING)
(define NK_INCLUDE_DEFAULT_FONT)
(define NK_IMPLEMENTATION)
(define NK_SDL3_RENDERER_IMPLEMENTATION)
(define NK_INCLUDE_STANDARD_BOOL)
(include Nuklear/nuklear.h)
(include nuklear_sdl3_renderer.h)

(define PROGNAME "travma")
(define VERSION "0.1.0")

(struct color
  ((r g b a float)))

(struct settings
  ((border-x u32)
   (l r t b x y w h u32)
   (max-width max-height u32)
   (colors [struct color 16])
   (selected-vertex i32)))

(struct video
  ((window (* SDL-Window))
   (renderer (* SDL-Renderer))
   (texture  (* SDL-Texture))))

(struct nine-grid
  ((l r t b float)
   (indices [u16 54])
   (verts [(struct vertex
             ((x y u v r g b a float))
             (attribute packed)) 16])))

(struct state
  ((settings (struct settings))
   (video (struct video))
   (nine-grid (struct nine-grid))
   (nk-ctx (* struct nk-context))
   (bg (struct nk-colorf))
   (running bool)))

(defmacro (sdl-try call desc)
  `(if (! ,call)
       (begin
        (SDL-Log "Oops")
        (SDL-Log "Failed to %s: %s" ,desc (SDL-GetError))
        (return 1))))

(fn render ((state (* struct state))) int
  (var now (const Uint64) (SDL-GetTicks))
  (var r (* SDL-Renderer) state->video.renderer)
  (var s (* struct settings) (& state->settings))

  (SDL-SetRenderDrawColor r 0 0 0 SDL-ALPHA-OPAQUE)
  (SDL-RenderClear r)

  (SDL-SetRenderDrawColor r 128 64 255 SDL-ALPHA-OPAQUE)
  (SDL-RenderLine state->video.renderer s->l 0.0f s->l s->max-height)
  (SDL-RenderLine state->video.renderer s->r 0.0f s->r s->max-height)
  (SDL-RenderLine state->video.renderer 0.0f s->t s->max-width s->t)
  (SDL-RenderLine state->video.renderer 0.0f s->b s->max-width s->b)

  (nk-sdl-render NK-ANTI-ALIASING-ON)

  (SDL-RenderPresent r)
  (return 0))

(defmacro (labeled-slider label . slider-args)
  `((nk-layout-row-dynamic s->nk-ctx 25 2)
    (nk-label s->nk-ctx ,label NK-TEXT-LEFT)
    (nk-slider-int s->nk-ctx ,@slider-args)))

(fn draw-gui ((s (* struct state))) void
  (if (nk-begin s->nk-ctx "Menu" (nk-rect 50 50 300 400)
                (c-bit-or NK-WINDOW-BORDER
                          NK-WINDOW-MOVABLE
                          NK-WINDOW-MINIMIZABLE
                          NK-WINDOW-TITLE))
      (begin
       (labeled-slider "Left" 1 (& s->settings.l) s->settings.max-width 1)
       (labeled-slider "Right" 1 (& s->settings.r) s->settings.max-width 1)
       (labeled-slider "Top" 1 (& s->settings.t) s->settings.max-height 1)
       (labeled-slider "Bottom" 1 (& s->settings.b) s->settings.max-height 1)
       (labeled-slider "X" 1 (& s->settings.x) 800 1)
       (labeled-slider "Y" 1 (& s->settings.y) 600 1)
       (labeled-slider "Width" 1 (& s->settings.w) 800 1)
       (labeled-slider "Height" 1 (& s->settings.h) 600 1)
       (if (nk-combo-begin-color s->nk-ctx (nk-rgb-cf s->bg) (nk-vec2 (nk-widget-width s->nk-ctx) 400))
           (begin
            (nk-layout-row-dynamic s->nk-ctx 120 1)
            (= s->bg (nk-color-picker s->nk-ctx s->bg NK-RGB))
            (nk-layout-row-dynamic s->nk-ctx 25 1)
            (= s->bg.r (nk-propertyf s->nk-ctx "#R:" 0 s->bg.r 1.0f 0.01f 0.005f))
            (= s->bg.g (nk-propertyf s->nk-ctx "#G:" 0 s->bg.g 1.0f 0.01f 0.005f))
            (= s->bg.b (nk-propertyf s->nk-ctx "#B:" 0 s->bg.b 1.0f 0.01f 0.005f))
            (= s->bg.a (nk-propertyf s->nk-ctx "#A:" 0 s->bg.a 1.0f 0.01f 0.005f))
            (nk-combo-end s->nk-ctx)))))
  (nk-end s->nk-ctx))

(fn main-loop ((s (* struct state))) bool
  (var e SDL-Event)
  (nk-input-begin s->nk-ctx)
  (while (SDL-PollEvent (& e))
    (switch e.type
            ((SDL-EVENT-QUIT)
             (SDL-Log "Got Quit event, bye...")
             (return false))
            ((SDL-EVENT-KEY-DOWN)
             (switch e.key.key
                     ((SDLK-ESCAPE)
                      (SDL-Log "Escape pressed, quitting. Bye...")
                      (return false)))))
    (nk-sdl-handle-event (& e)))
  (nk-input-end s->nk-ctx)

  (draw-gui s)

  (render s)
  (return true))

(pub fn main ((argc int) (argv [const * char])) int
  (SDL-Log "Starting %s v%s" PROGNAME VERSION)
  (var state (struct state))
  (= state.settings.l 138)
  (= state.settings.r 454)
  (= state.settings.t 101)
  (= state.settings.b 102)
  (= state.settings.x 0)
  (= state.settings.y 0)
  (= state.settings.w 100)
  (= state.settings.h 100)
  (= state.settings.max-width 1000)
  (= state.settings.max-height 1000)

  (sdl-try (SDL-Init SDL-INIT-VIDEO) "initialize video")
  (sdl-try (SDL-CreateWindowAndRenderer PROGNAME 800 600 0 (& state.video.window) (& state.video.renderer))
           "create window/renderer")

  ;; Nuklear initialization
  (= state.nk-ctx (nk-sdl-init state.video.window state.video.renderer))
  (= state.bg (cast #((= .r 0.10f)
                      (= .g 0.18f)
                      (= .b 0.24f)
                      (= .a 1.0f))
                    (struct nk-colorf)))
  (begin
   (var atlas (* struct nk-font-atlas))
   (var config (struct nk-font-config) (nk-font-config 0))
   (var font (* struct nk-font))
   (nk-sdl-font-stash-begin (& atlas))
   (= font (nk-font-atlas-add-default atlas 13 (& config)))
   (nk-sdl-font-stash-end)
   (nk-style-set-font state.nk-ctx (& font->handle)))

  (= state.running true)
  (while state.running
    (= state.running (main-loop (& state))))
  (nk-sdl-shutdown)
  (return 0))
